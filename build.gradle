plugins {
    id 'java-library'
    id 'net.neoforged.moddev' version '2.0.113'
    id 'idea'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '7.0.2'
    id 'com.palantir.git-version' version '4.0.0' apply false
}

group = maven_group
// Formats the mod version to include the loader, Minecraft version, and '-SNAPSHOT-<timestamp>' if enabled.

if (project.file('.git/HEAD').isFile()) {
    apply plugin: 'com.palantir.git-version'
}

String identifiedVersion
String versionOverride = System.getenv("VERSION") ?: null
try {
    identifiedVersion = versionOverride == null ? gitVersion() : versionOverride
}
catch (Exception ignored) {
    out.style(Style.Failure).text(
        'This mod must be version controlled by Git AND the repository must provide at least one tag,\n' +
            'or the VERSION override must be set! ').style(Style.SuccessHeader).text('(Do NOT download from GitHub using the ZIP option, instead\n' +
        'clone the repository'
    )
    versionOverride = 'NO-GIT-TAG-SET'
    identifiedVersion = versionOverride
}

mod_version = identifiedVersion

version = "${mod_version}"
boolean isCI = System.getenv("CI") != null

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

//neoForge.version = libs.versions.neo.get()

sourceSets {
    main {
        resources {
            srcDir 'src/generated/resources'
        }
    }
    buildtools
}

configurations {
    // set these as unresolvable so we don't accidentally force all dependents to use them with us
    clientRuntimeOnly.canBeResolved = false
    extraRuntimeOnly.canBeResolved = false
    clientExtraRuntimeOnly.canBeResolved = false

    runtimeClasspath.extendsFrom localRuntime
    extraRuntimeClasspath.extendsFrom extraLocalRuntime
    clientRuntimeClasspath.extendsFrom clientLocalRuntime
    extraClientRuntimeClasspath.extendsFrom clientExtraLocalRuntime

}

afterEvaluate {
    configurations {
        // same as above but for the modRuntimeOnly configurations
        modClientRuntimeOnly.canBeResolved = false
        modExtraRuntimeOnly.canBeResolved = false
        modClientExtraRuntimeOnly.canBeResolved = false
    }
}

apply from: "$rootDir/gradle/scripts/jars.gradle"
apply from: "$rootDir/gradle/scripts/moddevgradle.gradle"
apply from: "$rootDir/gradle/scripts/repositories.gradle"
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/gradle/scripts/resources.gradle"
apply from: "$rootDir/gradle/scripts/publishing.gradle"
apply from: "$rootDir/gradle/scripts/spotless.gradle"


tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:-removal"
    options.compilerArgs << "-Aquiet=true" // Suppress mixin notes
}
